archetype (adl_version=1.4; uid=2275acea-1919-44d6-a342-42e6b5c77185)
	openEHR-EHR-EVALUATION.mental_capacity.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2020-04-22">
		["name"] = <"Heidi Koikkalainen">
		["organisation"] = <"freshEHR Clinical Informatics Ltd.">
		["email"] = <"heidi@freshehr.com">
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Ian McNicoll, freshEHR Clinical Informatics Ltd.", ...>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about the ability of an individual to use and understand information to make a decision or plan.">
			keywords = <"mental, capacity, competency, competent, decision", ...>
			copyright = <"Â© openEHR Foundation">
			use = <"Use to record details about the ability of an individual to use and understand information to make a decision or plan.

This archetype is intended to capture information about the mental capacity status of an individual, which is typically assessed in relation to a specific decision or plan that needs to be made about their medical care or treatment at a particular time. If specific details about the mental capacity assessments are required, additional archetypes can be nested within the 'Assessment details' SLOT.">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Think Capacity, Think Consent [Internet]. NHS Education for Scotland. 2020 [cited 2020 May 12]. Available from: https://www.nes.scot.nhs.uk/media/1557644/capacity_and_consent-interactive.pdf

What is capacity? [Internet]. Compassion in Dying. 2020 [cited 2020 May 12]. Available from: https://compassionindying.org.uk/making-decisions-and-planning-your-care/treatment-care-decisions/about-capacity/.">
		["current_contact"] = <"Heidi Koikkalainen, heidi@freshehr.com">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"C273D8022541A67F6E9B1EC57CE9D53E">
		["build_uid"] = <"28449b8b-e69b-4659-bf36-a7c4592c2861">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Mental capacity
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0009] occurrences matches {0..1} matches {    -- Decision/plan
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0002] occurrences matches {0..1} matches {    -- Status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0003,    -- Has capacity
									at0004,    -- Does not have capacity
									at0005]    -- Indeterminate
								}
							}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0008] occurrences matches {0..*} matches {    -- Assessment
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0028] occurrences matches {0..1} matches {    -- Valid period start
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0027] occurrences matches {0..1} matches {    -- Valid period end
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							allow_archetype CLUSTER[at0012] occurrences matches {0..1} matches {    -- Assessor
								include
									archetype_id/value matches {/.*/}
							}
							allow_archetype CLUSTER[at0013] occurrences matches {0..*} matches {    -- Person consulted
								include
									archetype_id/value matches {/.*/}
							}
							allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {    -- Assessment details
								include
									archetype_id/value matches {/.*/}
							}
						}
					}
					ELEMENT[at0022] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0023] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0024] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Mental capacity">
					description = <"The ability of an individual to use and understand information to make a decision or plan.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Status">
					description = <"The individual's mental capacity status.">
				>
				["at0003"] = <
					text = <"Has capacity">
					description = <"The individual has sufficient mental capacity to make the decision or plan.">
				>
				["at0004"] = <
					text = <"Does not have capacity">
					description = <"The individual does not have sufficient mental capacity to make the decision or plan.">
				>
				["at0005"] = <
					text = <"Indeterminate">
					description = <"It has not been able to determinate whether the individual has sufficient mental capacity to make decision or plan.">
				>
				["at0006"] = <
					text = <"Description">
					description = <"Narrative description of the individual's mental capacity or incapacity.">
				>
				["at0008"] = <
					text = <"Assessment">
					description = <"Details of a mental capacity assessment undertaken in relation to the specific decision or plan.">
				>
				["at0009"] = <
					text = <"Decision/plan">
					description = <"Description of the specific decision or plan to which the mental capacity status and assessment details relate.">
				>
				["at0012"] = <
					text = <"Assessor">
					description = <"Details of the person carrying out the mental capacity assessment.">
				>
				["at0013"] = <
					text = <"Person consulted">
					description = <"Details of a person who has been consulted in relation to the mental capacity assessment.">
				>
				["at0014"] = <
					text = <"Assessment details">
					description = <"Additional structured details about the mental capacity assessment.">
				>
				["at0022"] = <
					text = <"Comment">
					description = <"Additional narrative about the individual's mental capacity or incapacity not captured in other fields.">
				>
				["at0023"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0024"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0027"] = <
					text = <"Valid period end">
					description = <"The date/time that marks the conclusion of the valid period of time for this mental capacity assessment.">
				>
				["at0028"] = <
					text = <"Valid period start">
					description = <"The date/time that marks the beginning of the valid period of time for this mental capacity assessment.">
				>
			>
		>
	>
