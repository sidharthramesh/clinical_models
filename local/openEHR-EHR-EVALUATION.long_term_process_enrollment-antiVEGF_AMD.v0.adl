archetype (adl_version=1.4; uid=36f8f3b8-572e-4337-8a11-810b8f657ba8)
	openEHR-EHR-EVALUATION.long_term_process_enrollment-antiVEGF_AMD.v0
specialize
	openEHR-EHR-EVALUATION.long_term_process_enrollment.v0

concept
	[at0000.1]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2015-06-24">
		["name"] = <"Aitor Eguzkitza">
		["organisation"] = <"Universidad Pública de Navarra - Complejo Hospitalario de Navarra">
		["email"] = <"aitor.eguzkiza@unavarra.es">
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Luis Serrano, Universidad Pública de Navarra (UPNA), lserrano@unavarra.es","José Andonegui, Hospital de Navarra, jose.andonegui.navarro@cfnavarra.es","Jesús D. Trigo, Universidad Pública de Navarra (UPNA), jesusdaniel.trigo@unavarra.es">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Record a contribution to the decision of admittance of a patient in the process of wet AMD treatment by intravitreal anti-VEGF periodical injections.">
			keywords = <"chronic","admittance","wet amd","anti-vegf">
			copyright = <"© openEHR Foundation">
			use = <"It can be used for enrollment of new patients into the cycles of anti-VEGF injections to treat wet AMD, but also it is possible to evaluate already registered patients to reconsider the continuity of their treatment at a given time.">
			misuse = <"Most of archetypes are focused on register acute episodes identified on clinical environment. Conversely, this one is aimed at periodically review a decision made about a protracted healthcare process (for instance, a therapeutic decision for a chronic condition that requires regular treatment). Thus, this archetype is not intended for registering decisions about occasional actions.">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"5B07C7E1EB58430EEF14CA0485911F66">
		["build_uid"] = <"cc8da233-b399-4189-bb59-1e017fd71d17">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000.1] matches {    -- Enrollment in intravitreal anti-VEGF therapy for wet AMD
		data matches {
			ITEM_TREE[at0001] matches {    -- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0004] matches {    -- Healthcare process
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0012] occurrences matches {0..1} matches {    -- Admittance
						value matches {
							DV_BOOLEAN matches {*}
						}
					}
					ELEMENT[at0010.1] occurrences matches {0..1} matches {    -- Criteria
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0.13,    -- Very low VA
									at0.14,    -- Healed lesions
									at0.15,    -- Multimorbidity
									at0.16,    -- Reaction to anti-VEGF
									at0.17,    -- VA decrease
									at0.18]    -- Morphologic deterioration
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0002] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0003] occurrences matches {0..1} matches {    -- Date decision
						value matches {
							DV_DATE_TIME matches {
								value matches {yyyy-mm-ddTHH:MM:SS}
							}
						}
					}
					ELEMENT[at0011] occurrences matches {0..1} matches {    -- Decision review
						value matches {
							DV_DATE matches {
								value matches {yyyy-mm-dd}
							}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0.13"] = <
					text = <"Very low VA">
					description = <"Visual acuity on first clinical encounter below 0.1 in decimal scale.">
				>
				["at0.14"] = <
					text = <"Healed lesions">
					description = <"Identified signs of previous treatments such as photodynamic therapy, laser photocoagulation scars, or any vitreoretinal operative procedure.">
				>
				["at0.15"] = <
					text = <"Multimorbidity">
					description = <"Coexistence of additional alterations on the retina causative of visual impairment.">
				>
				["at0.16"] = <
					text = <"Reaction to anti-VEGF">
					description = <"Establishment or suspicion of any reaction due to hypersensitivity to anti-VEGF agents used in intravitreal injections.">
				>
				["at0.17"] = <
					text = <"VA decrease">
					description = <"Visual acuity decreased for three consecutive reviews below 0.1 in decimal scale.">
				>
				["at0.18"] = <
					text = <"Morphologic deterioration">
					description = <"Progressive deterioration of the morphology of the lesion.">
				>
				["at0000"] = <
					text = <"Enrollment in a long-term healthcare process">
					description = <"Manages the enrollment of patients in a specific long-term healthcare process.">
				>
				["at0000.1"] = <
					text = <"Enrollment in intravitreal anti-VEGF therapy for wet AMD">
					description = <"Manages the enrollment of patients in intravitreal anti-VEGF therapy to treat wet AMD.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Date decision">
					description = <"Date at which decision was made about the enrollment or exclusion of the patient with regard to the healthcare process.">
				>
				["at0004"] = <
					text = <"Healthcare process">
					description = <"Identification of the healthcare process about which the enrollment of a specific patient is discussed.">
				>
				["at0005"] = <
					text = <"Description">
					description = <"Narrative description about the healthcare service to which the patient has been proposed for admittance.">
				>
				["at0010"] = <
					text = <"Criteria">
					description = <"Narrative description of criteria considered to make a decision with regard to patient's admittance into the healthcare process.">
				>
				["at0010.1"] = <
					text = <"Criteria">
					description = <"Different criteria considered to make a decision with regard to patient's admittance into the healthcare process.">
				>
				["at0011"] = <
					text = <"Decision review">
					description = <"Next revision date scheduled for the current decision of enrollment.">
				>
				["at0012"] = <
					text = <"Admittance">
					description = <"If true, the patient meets the criteria required to be inscribed in the healthcare process.">
				>
			>
		>
	>
